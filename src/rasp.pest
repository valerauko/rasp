COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | NEWLINE | "," | "\t" }

boolean = { "true" | "false" }

char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" | ("u" ~ ASCII_HEX_DIGIT{4})
}
charseq = @{ char* }
string = ${ "\"" ~ charseq ~ "\"" }

NAMESYM = {
  "*" | "+" | "!" | "-" | "_" | "?" | "%" | "<" | ">" | "="
}
segment = @{ (XID_START | NAMESYM) ~ (XID_CONTINUE | NAMESYM)* }
slash = @{ "/" }
access = @{ "." ~ segment? }
symbol = ${
  (
    slash
    | access
    | (
      segment ~ (
        ("." ~ segment)* ~ (
          slash ~ (slash | segment)
        )?
      )
    )
  ) ~ !(XID_CONTINUE | NAMESYM | "." | "/")
}
string_keyword = ${ ((segment ~ ("." ~ segment)* ~ slash)? ~ string) }
keyword = ${ ":" ~ ":"? ~ (symbol | string_keyword) }

vector = { "[" ~ expr* ~ "]" }
map = { "{" ~ (expr ~ expr)* ~ "}"}
list = { "(" ~ expr* ~ ")" }

expr = { symbol | keyword | string | vector | map | list }

rasp = { SOI ~ expr+ }
