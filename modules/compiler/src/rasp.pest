COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{ " " | NEWLINE | "," | "\t" }

boolean = { "true" | "false" }

char = {
  !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" | ("u" ~ ASCII_HEX_DIGIT{4})
}
charseq = @{ char* }
string = ${ "\"" ~ charseq ~ "\"" }

NAMESYM = {
  "*" | "+" | "!" | "-" | "_" | "?" | "%" | "<" | ">" | "="
}
segment = @{ (XID_START | NAMESYM) ~ (XID_CONTINUE | NAMESYM)* }
slash = @{ "/" }

namespace = ${ segment ~ ("." ~ segment)* }
name = ${ slash | segment }

symbol = ${ (namespace ~ slash)? ~ name }
kw_alias = @{ ":" }
keyword = ${ ":" ~ kw_alias? ~ (namespace ~ slash)? ~ (name | string) }

vector = { "[" ~ expr* ~ "]" }
shorthand = { "{" ~ symbol* ~ "}" }
map = { "{" ~ shorthand? ~ (expr ~ expr)* ~ "}"}
list = { "(" ~ expr* ~ ")" }

decimal = @{ "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }

expr = { decimal | namespace | symbol | keyword | string | vector | map | list }

rasp = { SOI ~ expr+ }
